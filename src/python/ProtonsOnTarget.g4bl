#  lengths are in mm, fields is Tesla !  Momentum in MeV
 
#______________________________________________________________________

#------------------------------- Setup -------------------------------#
 
 
# Must add kill=$kill_aperture to every element to use this feature.
randomseed time
param kill_aperture=1
param -unset nEv=2000000000


 
#physics QGSP_BERT disable=Decay
physics QGSP_BERT


#---------------------------- Define World and Beam -------------------------#

# Set default environment material
param worldMaterial=Vacuum


#BEAM
# 400 MeV proton beam at 30.6 deg. from secondary-beamline direction aimed at target center
param -unset angle=30.6
beam gaussian rotation=Y(180+$angle) x=100*0.5090 y=0 z=100*0.8607 meanMomentum=954.15 sigmaP=0 nEvents=$nEv particle=proton sigmaX=3 sigmaY=3 meanXp=0.0 meanYp=0.0 sigmaXp=0 sigmaYp=0


# Only pay attention to the following particles (saves on calculation time)
particlecolor proton=1,0,0 pi-=0,1,0 mu-=0,0,1
trackcuts keep=mu-,pi-,proton

 

#--------------------------- Background Color --------------------------#

 
# Scaling GUI view from pg 19 of the manual
 
g4ui when=4 "/vis/viewer/scaleTo 5 5 1"


#--------------------------- Define Beamline Elements --------------------------#


# Parameters
param Tw=15 Th=30 Tl=30
param Tm=W

param QUADfieldlength13=1076.3
param QUADgradient13=0.140
param QUADironLength13=1076.3

param QUADfieldlength2=1452.6
param QUADgradient2=-0.155
param QUADironLength2=1452.6

param QUADapertureRadius=49.15
param QUADironRadius=50.8

param SOLinnerRadius=20.0
param SOLouterRadius=22.0
param SOLlength=130.0
param SOLmaxZ=64.0
param -unset SOLcurrent=158.2

box Target width=$Tw height=$Th length=$Tl material=Tm color=1.0,0.75,0.0,.25 


genericquad Quad2 fieldLength=$QUADfieldlength2 apertureRadius=$QUADapertureRadius ironLength=$QUADironLength2 \
ironRadius=$QUADironRadius ironColor=0,0,1,.3 \
gradient=$QUADgradient2 fringeFactor=10.0 kill=$kill_aperture


genericquad Quad13 fieldLength=$QUADfieldlength13 apertureRadius=$QUADapertureRadius ironLength=$QUADironLength13 \
ironRadius=$QUADironRadius ironColor=0,0,1,.3 \
gradient=$QUADgradient13 fringeFactor=10.0 kill=$kill_aperture


coil LLS innerRadius=$SOLinnerRadius outerRadius=$SOLouterRadius length=$SOLlength 


solenoid LomaLindaSolenoid coilName=LLS current=$SOLcurrent  color=1,0,0,.3 kill=$kill_aperture


tubs TargetToSolenoid material=STAINLESS-STEEL outerRadius=70.0 innerRadius=60.0 length=355.6  color=.2,.2,.2,.3 kill=$kill_aperture

tubs SolenoidToQUAD1 material=STAINLESS-STEEL outerRadius=50.8 innerRadius=49.15 length=231.45  color=.2,.2,.2,.3 kill=$kill_aperture

tubs QUADGap material=STAINLESS-STEEL outerRadius=50.8 innerRadius=49.15 length=304.8  color=.2,.2,.2,.3 kill=$kill_aperture

tubs QUAD3ToStraw material=STAINLESS-STEEL outerRadius=50.8 innerRadius=49.15 length=388.9  color=.2,.2,.2,.3 kill=$kill_aperture

tubs Straw material=STAINLESS-STEEL outerRadius=17.525 innerRadius=15.875 length=585.2  color=.2,.2,.2,.3 kill=$kill_aperture



#---------------------- Define Virtual Detectors for scoring -----------------------#

virtualdetector Det radius=50 length=1 color=1,1,1 material=Vacuum format=ASCII require=PDGid!=2212

virtualdetector muDet radius=15.875 length=1 color=1,1,1 material=Vacuum format=ASCII require=PDGid!=2212

virtualdetector piDet radius=50.8 length=1 color=1,1,1 material=Vacuum format=ASCII require=PDGid!=2212

virtualdetector solenoidDet radius=31.75 length=1 color=1,1,1 material=Vacuum format=ASCII require=PDGid!=2212

#---------------------- Place Beamline Elements -----------------------#

place Target rename=target z=0.0 rotation=Y(180+$angle)
place LomaLindaSolenoid z=435.6
place SolenoidToQUAD1 rename=S2Q z=616.325
place Quad13 rename=Q1 z=1270.2
place QUADGap rename=QGap12 z=1960.75
place Quad2 rename=Q2 z=2839.4125
place QUADGap rename=QGap23 z=3718.075
place Quad13 rename=Q3 z=4408.625
place QUAD3ToStraw rename=Q2Str z=5141.225
place Straw z=5628.275

#---------------------- Place Virtual Detectors -----------------------#
param decay=On
place piDet rename=batch2|piDet|Decay$decay|nEv$nEv z=20
place solenoidDet rename=batch2|solDet|Decay$decay|nEv$nEv z=503
place Det rename=batch2|Q3Det|Decay$decay|nEv$nEv z=4946.225
place Det rename=batch2|endDet z=5921
